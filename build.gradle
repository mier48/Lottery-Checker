buildscript {
    repositories {
        google()
        mavenCentral()
//        maven {
//            url "https://plugins.gradle.org/m2/"
//        }
    }

    dependencies {
        classpath "com.google.gms:google-services:4.3.14"
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

plugins {
    alias libs.plugins.com.android.application apply false
    alias libs.plugins.com.android.library apply false
    alias libs.plugins.org.jetbrains.kotlin.android apply false
    alias libs.plugins.com.google.dagger.hilt.android apply false
    alias libs.plugins.com.github.ben.manes.versions
    alias libs.plugins.nl.littlerobots.version.catalog.update
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}